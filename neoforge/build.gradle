plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}
dependencies {
    implementation project(':common')
    compileOnly "mezz.jei:jei-1.21.9-common-api:25.0.0.2"
    compileOnly "mezz.jei:jei-1.21.9-neoforge-api:25.0.0.2"
    runtimeOnly  "mezz.jei:jei-1.21.9-neoforge:25.0.0.2"
}

tasks.processResources {
    from(project(':common').sourceSets.main.resources) {
        include 'data/**'
        include 'assets/**'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    
    srcDir project(':common').sourceSets.main.resources
    
    exclude 'pack.mcmeta'
}

tasks.named('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}